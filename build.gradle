//file:noinspection GroovyAssignabilityCheck

plugins {
    id 'net.minecraftforge.gradle' version '5.1.69'
    id 'com.github.johnrengelman.shadow' version '7.1.+'
    id 'wtf.gofancy.fancygradle' version '1.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

fancyGradle {
    patches {
        resources
        coremods
        asm
        mergetool
    }
}

version = "${project.minecraft_version}-${project.mod_version}"
group = project.maven_group
archivesBaseName = project.base_name

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'stable', version: project.mappings_version
    accessTransformer = file('src/main/resources/META-INF/projectdata_at.cfg')
    runs {
        def args = [
                '-Dmixin.debug.export=true',
                '-Dmixin.hotSwap=true',
                '-Dmixin.checks.interfaces=true'
        ]
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }
        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }
    }
}


repositories {
    maven {
        url = uri('https://www.cursemaven.com')
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        name = 'Modrinth'
        url = uri('https://api.modrinth.com/maven')
        content {
            includeGroup 'maven.modrinth'
        }
    }
    maven {
        name = 'Sponge'
        url = uri('https://repo.spongepowered.org/maven')
    }
    flatDir {
        dirs 'build/libs'
    }
}

configurations {
    shade
    implementation.extendsFrom shade
    shadow.extendsFrom shade
}

dependencies {
    //minecraft
    minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    //mixin
    shade("org.spongepowered:mixin:${project.mixin_version}") {
        exclude module: 'asm-commons'
        exclude module: 'asm-tree'
        exclude module: 'commons-io'
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'launchwrapper'
        exclude module: 'log4j-core'
    }
    annotationProcessor("org.spongepowered:mixin:${project.mixin_version}:processor")

    //required mod dependencies
    //implementation(fg.deobf("maven.modrinth:the-impossible-library:${project.minecraft_version}-${project.lib_version}"))
    //local versions of required dependencies for testing
    //noinspection DependencyNotationArgument
    implementation name: "theimpossiblelibrary-${project.minecraft_version}-${project.lib_version}"

    //mod support dependencies
    implementation(fg.deobf("curse.maven:had-enough-items-557549:${project.hei_version}"))
	
	//only needed for runtime
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier ''
    mergeServiceFiles()
}

reobf {
    shadowJar {
        dependsOn createMcpToSrg
        mappings = createMcpToSrg.output
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft_version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft_version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, 'projectdata.refmap.json'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(configurations.shade.collect {
        it.isDirectory() ? it : zipTree(it)
    }) {
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    manifest {
        attributes([
                'Specification-Version': project.version,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'FMLCorePlugin': 'mods.thecomputerizer.projectdata.core.MixinLoadingPlugin',
                'FMLCorePluginContainsFMLMod': true,
                //'ForceLoadAsMod': true,
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': 'projectdata.mixin.json',
                'FMLAT': 'projectdata_at.cfg'
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.assemble.dependsOn shadowJar